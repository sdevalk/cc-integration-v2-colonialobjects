###############################
# Query for generating a search graph from dataset 'Colonial Objects'
###############################

PREFIX cc: <https://colonialcollections.nl/schema#> # Internal ontology
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX la: <https://linked.art/ns/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?this a cc:HeritageObject ;
    cc:additionalType ?typeName ;
    cc:identifier ?identifier ;
    cc:name ?name ;
    cc:description ?description ;
    cc:material ?materialName ;
    cc:publisher ?publisherName ;
    cc:creator ?creatorName ;
    cc:locationCreated ?locationCreatedName ;
    cc:alternateName ?alternateLocationCreatedName ; # Only for indexing
    cc:countryCreated ?countryCreatedName .
}
WHERE {
  BIND(?_iri AS ?this)

  ?this a crm:E22_Human-Made_Object .

  ####################
  # Type
  ####################

  OPTIONAL {
    ?this crm:P2_has_type [
      rdfs:label ?typeName
    ] ;
  }

  ####################
  # Subject
  ####################
  # TBD: not in the KG yet (crm:P62_depicts)

  ####################
  # Material
  ####################

  OPTIONAL {
    ?this crm:P45_consists_of [
      rdfs:label ?materialName
    ] ;
  }

  ####################
  # Identifier
  ####################

  OPTIONAL {
    ?this crm:P1_is_identified_by [
      a crm:E42_Identifier ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404626> ; # Identification number
      crm:P190_has_symbolic_content ?identifier
    ] ;
  }

  ####################
  # Name
  ####################

  OPTIONAL {
    ?this crm:P1_is_identified_by [
      a crm:E33_E41_Linguistic_Appellation ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404670> ; # Name
      crm:P190_has_symbolic_content ?name
    ] ;
  }

  ####################
  # Description
  ####################

  OPTIONAL {
    ?this crm:P67i_is_referred_to_by [
      crm:P2_has_type <http://vocab.getty.edu/aat/300435416> ; # Description
      crm:P190_has_symbolic_content ?description
    ] ;
  }

  ####################
  # Inscription
  ####################
  # TBD: not in the KG yet (crm:P128_carries)

  ####################
  # Technique
  ####################
  # TBD: not in the KG yet (crm:P108i_was_produced_by/crm:P32_used_general_technique)

  ####################
  # Part of dataset
  ####################

  # TODO: 'la:member_of' does not point to the source dataset
  # ?this la:member_of ?dataset .
  BIND(<https://linkeddata.cultureelerfgoed.nl/colonialheritage/colonialobjects> AS ?dataset)

  ####################
  # Publisher of dataset
  ####################

  OPTIONAL {
    ?dataset schema:publisher [
      schema:name ?publisherName ;
    ] ;
  }

  ####################
  # Creator
  ####################

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P14_carried_out_by [
      rdfs:label ?creatorName
    ] ;
  }

  ####################
  # Date of creation
  ####################
  # TBD: not in the KG yet (crm:P108i_was_produced_by/crm:P4_has_time-span/crm:P82a_begin_of_the_begin, crm:P108i_was_produced_by/crm:P4_has_time-span/crm:P82b_end_of_the_end)

  ####################
  # Location of creation
  ####################

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P7_took_place_at ?location .
    ?location gn:name ?locationCreatedName ;
      gn:featureCode ?featureCode .
    FILTER(LANG(?locationCreatedName) = "" || LANGMATCHES(LANG(?locationCreatedName), "en"))

    OPTIONAL {
      ?location gn:alternateName ?alternateLocationCreatedName
      FILTER(LANG(?alternateLocationCreatedName) = "" || LANGMATCHES(LANG(?alternateLocationCreatedName), "en"))
    }

    # Country of which the location is a part
    OPTIONAL {
      ?location gn:parentCountry ?country .
      ?country gn:name ?tmpCountryCreatedName .
      FILTER(LANG(?tmpCountryCreatedName) = "" || LANGMATCHES(LANG(?tmpCountryCreatedName), "en"))
    }

    # Location could be a country
    # https://www.geonames.org/export/codes.html
    BIND(IF(?featureCode = <https://www.geonames.org/ontology#A.PCLI>, ?locationCreatedName, ?tmpCountryCreatedName) AS ?countryCreatedName)
  }
}
